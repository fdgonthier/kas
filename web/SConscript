# Hey, Emacs! This is a -*- Python -*- file!
#
# Copyright (C) 2006-2012 Opersys inc.
# 
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

Import('BUILD_ENV opts_dict')
import os

wwwParent = "/" + apply(os.path.join, opts_dict['WWWDIR'].split(os.path.sep)[:-1])
wwwRoot = opts_dict['WWWDIR']

# Create the hierarchy of the directories to be created on the target. 
def InstallTree(treeDir, baseDir):
    install_list = []
    for s in os.walk(treeDir):
        (base, dirs, files) = s
        for d in dirs:
            sourceDir = Dir(os.path.join(base, d))
            targetDir = Dir(os.path.join(baseDir, base))
            # Don't try to reinstall the directory if it exists.
            if not os.path.exists(str(targetDir)):
                install_list.append(BUILD_ENV.Install(targetDir, sourceDir))
        for f in files:
            (_, ext) = os.path.splitext(f)
            if ext in [".pyc"]:
                continue
            if f in ["Makefile", "cscope.files"]:
                continue
            if f.endswith(".mako.py"):
                continue
            sourceFile = File(os.path.join(base, f))
            targetDir = Dir(os.path.join(baseDir, base))
            install_list.append(BUILD_ENV.Install(targetDir, sourceFile))
    return install_list

# The static data is installed in the root of the web directory.
staticTree = InstallTree('www', wwwParent)

# Individual application create their own directories.
kas_cfg = InstallTree('kas_cfg', wwwRoot)
freemium = InstallTree('freemium', wwwRoot)
kwmo = InstallTree('kwmo', wwwRoot)

tf_env = BUILD_ENV.Clone()
tf_env.Append(PYTHONDIR = opts_dict['PYTHONDIR'],
              WWWDIR = opts_dict['WWWDIR'])

# Predefine LINESEPARATOR or Textfile will choke.
tf_env['LINESEPARATOR'] = os.linesep

# Install the platform configuration file for the web app.
cfg_kas = tf_env.Textfile(File(os.path.join('kas_cfg', 'teambox_cfg.py')),
                          ["SYS_PATHS=['$PYTHONDIR']",
                           "SITE_DIRS=['$WWWDIR/kas_cfg']"])
cfg_kwmo = tf_env.Textfile(File(os.path.join('kwmo', 'teambox_cfg.py')),
                           ["SYS_PATHS=['$PYTHONDIR\']",
                            "SITE_DIRS=['$WWWDIR/kwmo']"])
cfg_freemium = tf_env.Textfile(File(os.path.join('freemium', 'teambox_cfg.py')),
                               ["SYS_PATHS=['$PYTHONDIR']",
                                "SITE_DIRS=['$WWWDIR/freemium']"])

install_list = staticTree + kas_cfg + freemium + kwmo 
install_list = install_list + cfg_kas + cfg_kwmo + cfg_freemium

Return('install_list')
                        
